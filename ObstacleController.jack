class ObstacleController {
    field DinoGame game;
    field Obstacle head;
    field Obstacle tail;
    field int nextObstacleInterval;
    field int minObsInterval, maxObsInterval;
    field Randomizer randomizer;

    constructor ObstacleController new(DinoGame currGame, Randomizer r) {
        let game = currGame;
        let head = Obstacle.new(this, 0, 0);
        let tail = Obstacle.new(this, 0, 0);
        do head.setNext(tail);
        do tail.setPrev(head);
        let randomizer = r;
        let minObsInterval = 200;
        let maxObsInterval = 400;
        let nextObstacleInterval = randomizer.randInt(minObsInterval, maxObsInterval);
        return this;
    }

    method Dino getDino() {
        return game.getDino();
    }

    method void stopGame() {
        do game.stop();
        return;
    }

    method void incScore() {
        do game.incScore();
        return;
    }

    method void decInterval() {
        if (minObsInterval > 100) {
            let minObsInterval = minObsInterval - 2;
        }
        if (maxObsInterval > 150) {
            let maxObsInterval = maxObsInterval - 2;
        }
        return;
    }

    method void move(int speed) {
        var Obstacle current;
        do checkNewObstacle();
        let current = head.getNext();
        while (~(current = tail)) {
            if (current.canMoveFurther(speed)) {
                do current.move(speed);
            }
            else {
                do removeObstacle(current);
            }
            
            let current = current.getNext();
        }
        return;
    }

    method void checkNewObstacle() {
        if (nextObstacleInterval = 0) {
            do addNewObstacle();
            let nextObstacleInterval = randomizer.randInt(minObsInterval, maxObsInterval);
        }
        let nextObstacleInterval = nextObstacleInterval - 1;
        return;
    }

    method void addNewObstacle() {
        var Obstacle new;
        var Obstacle prev;
        let new = Obstacle.new(this, randomizer.randInt(5, 30), randomizer.randInt(10, 50));
        let prev = tail.getPrev();
        do prev.setNext(new);
        do new.setPrev(prev);
        do new.setNext(tail);
        do tail.setPrev(new);
        return;
    }

    method void removeObstacle(Obstacle o) {
        var Obstacle prev, next;
        do o.erase();
        let prev = o.getPrev();
        let next = o.getNext();
        do prev.setNext(next);
        do next.setPrev(prev);
        do o.dispose();
        return;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }
}